// Top-level build file where you can add configuration options common to all sub-projects/modules.


plugins {
    id "org.jetbrains.kotlin.jvm" apply false
}
allprojects {
    repositories {
        mavenCentral()
    }
}

task publishAutoFabricToMavenLocal {
    group = "AutoFabric"
    dependsOn("ap:publishToMavenLocal")
    dependsOn("plugin:publishToMavenLocal")
}

task publishAutoFabric {
    group = "AutoFabric"
    dependsOn("ap:bintrayUpload")
    dependsOn("plugin:publishPlugins")
    dependsOn("updateTemplate")
}


task updateTemplate(type: Copy) {
    group = "template"
    description = "Switches out variables like \$total_version in .template.md files, and copies the results into .md files."
    from(rootDir)
    include("**/*.template.md")
    filesMatching("**/*.template.md") {
        def extensionLength = ".template.md".length()

        name = sourceName.substring(0, sourceName.length() - extensionLength) + ".md"
        expand("version":shared_version, "kotlin_version":kotlin_version)
    }
    destinationDir = rootDir
}
