plugins {
    id 'java'
    id "maven-publish"
    id 'java-gradle-plugin'
    id "org.jetbrains.kotlin.jvm"
    id "com.gradle.plugin-publish" version "0.11.0"
}

group 'com.github.fudge'
version shared_version

archivesBaseName = "autofabric"


repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
}


gradlePlugin {
    plugins {
        autoFabricPlugin {
            id = 'com.github.fudge.autofabric'
            implementationClass = 'fudge.AutoFabricPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/natanfudge/AutoFabric'
    vcsUrl = 'https://github.com/natanfudge/AutoFabric'
    description = 'Automates entrypoints'
    tags = ['fabric', 'annotation processor']

    plugins {
        autoFabricPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'AutoFabric'
        }
    }
}

processResources {
    inputs.property "version", shared_version
    from(sourceSets.main.resources.srcDirs) {
        include "version.txt"
        expand "version": shared_version
    }
}
